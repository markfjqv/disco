# Copyright 2013 The Flutter Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/compiled_action.gni")
import("//flutter/impeller/tools/impeller.gni")
import("//flutter/testing/testing.gni")

disco_root = "//flutter/impeller/disco"

template("disco_component") {
  impeller_component(target_name) {
    forward_variables_from(invoker, "*")

    if (!defined(public_configs)) {
      public_configs = []
    }

    public_configs += [ "$disco_root/:disco_public_config" ]
  }
}

template("disco_application") {
  assert(defined(invoker.dart_main), "The Dart main must be defined.")
  assert(defined(invoker.assets), "The assets must be defined.")
  assert(is_mac, "Disco only supports the Mac platform at this time.")

  fixture_target_name = "disco_fixtures_$target_name"
  test_fixtures(fixture_target_name) {
    dart_main = invoker.dart_main
    fixtures = invoker.assets
    fixtures += [ "//third_party/icu/common/icudtl.dat" ]
  }

  disco_component(target_name) {
    # TODO(csg): This is only necessary because we are stealing the test
    # harnesses snapshotters.
    testonly = true
    target_type = "executable"
    output_name = "disco_$target_name"
    deps = [
      ":$fixture_target_name",
      "//flutter/impeller/disco/shell",
    ]
  }
}

template("epoxy_native") {
  assert(defined(invoker.source),
         "The Epoxy bindings definition must be specified.")

  epoxy_file_name = get_path_info(invoker.source, "name")

  epoxy_cxx_path = "$target_gen_dir/$epoxy_file_name.cc"
  epoxy_cxx_template =
      "//flutter/impeller/disco/epoxy/template/cxx_impl.template.epoxy"

  epoxy_hdr_path = "$target_gen_dir/$epoxy_file_name.h"
  epoxy_hdr_template =
      "//flutter/impeller/disco/epoxy/template/cxx_interface.template.epoxy"

  cxx_target_name = "epoxy_cxx_$target_name"
  compiled_action(cxx_target_name) {
    tool = "//flutter/impeller/disco/epoxy"
    inputs = [ invoker.source ]
    outputs = [ epoxy_cxx_path ]
    args = [
      "--output",
      rebase_path(epoxy_cxx_path, root_build_dir),
      "--idl",
      rebase_path(invoker.source, root_build_dir),
      "--template-file",
      rebase_path(epoxy_cxx_template, root_build_dir),
    ]
  }

  hdr_target_name = "epoxy_hdr_$target_name"
  compiled_action(hdr_target_name) {
    tool = "//flutter/impeller/disco/epoxy"
    inputs = [ invoker.source ]
    outputs = [ epoxy_hdr_path ]
    args = [
      "--output",
      rebase_path(epoxy_hdr_path, root_build_dir),
      "--idl",
      rebase_path(invoker.source, root_build_dir),
      "--template-file",
      rebase_path(epoxy_hdr_template, root_build_dir),
    ]
  }

  disco_component(target_name) {
    sources = []

    sources += get_target_outputs(":$cxx_target_name")
    sources += get_target_outputs(":$hdr_target_name")

    deps = [ ":$cxx_target_name" ]
  }
}
